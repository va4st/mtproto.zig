const std = @import("std");
const builtin = @import("builtin");
const net = std.Io.net;
const posix = std.posix;
const linux = std.os.linux;
const proxy = @import("proxy/proxy.zig");
const config = @import("config.zig");
const version = @import("version").version;

const log = std.log.scoped(.metrics);
const metrics_content_type = "text/plain; version=0.0.4";

const ProcessMetrics = struct {
    resident_memory_bytes: ?u64 = null,
    virtual_memory_bytes: ?u64 = null,
    cpu_seconds_total: ?f64 = null,
    open_fds: ?u64 = null,
    max_fds: ?u64 = null,
    cgroup_memory_usage_bytes: ?u64 = null,
    cgroup_memory_limit_bytes: ?u64 = null,
};

pub fn start(state: *proxy.ProxyState) !void {
    if (builtin.os.tag != .linux) return error.UnsupportedOperatingSystem;

    const host = state.config.metrics.effectiveHost();
    const port = state.config.metrics.port;
    const io_ctx = std.Io.Threaded.global_single_threaded.io();

    const listen_addr = try resolveFirstAddress(host, port);
    var server = try listen_addr.listen(io_ctx, .{
        .reuse_address = true,
        .kernel_backlog = 64,
    });
    errdefer server.deinit(io_ctx);

    log.info("metrics endpoint listening on {s}:{d}", .{ host, port });

    const thread = try std.Thread.spawn(.{}, acceptLoop, .{ state, server });
    thread.detach();
}

fn acceptLoop(state: *proxy.ProxyState, server: net.Server) void {
    var local_server = server;
    const io_ctx = std.Io.Threaded.global_single_threaded.io();
    defer local_server.deinit(io_ctx);

    while (true) {
        const conn = local_server.accept(io_ctx) catch |err| {
            log.warn("metrics accept failed: {any}", .{err});
            sleepNs(200 * std.time.ns_per_ms);
            continue;
        };
        handleConnection(state, conn.socket.handle);
    }
}

fn resolveFirstAddress(host: []const u8, port: u16) !net.IpAddress {
    if (net.IpAddress.parse(host, port)) |literal| return literal else |_| {}

    const host_name = try net.HostName.init(host);
    const io_ctx = std.Io.Threaded.global_single_threaded.io();

    var results_buf: [8]net.HostName.LookupResult = undefined;
    var results: std.Io.Queue(net.HostName.LookupResult) = .init(&results_buf);
    try host_name.lookup(io_ctx, &results, .{ .port = port });

    while (results.getOneUncancelable(io_ctx)) |entry| {
        switch (entry) {
            .address => |addr| return addr,
            .canonical_name => {},
        }
    } else |err| switch (err) {
        error.Closed => {},
    }

    return error.AddressNotAvailable;
}

fn sleepNs(ns: u64) void {
    var req: posix.timespec = .{
        .sec = @intCast(ns / std.time.ns_per_s),
        .nsec = @intCast(ns % std.time.ns_per_s),
    };
    while (true) {
        var rem: posix.timespec = undefined;
        const rc = linux.nanosleep(&req, &rem);
        switch (linux.errno(rc)) {
            .SUCCESS => return,
            .INTR => req = rem,
            else => return,
        }
    }
}

fn handleConnection(state: *proxy.ProxyState, fd: posix.fd_t) void {
    defer closeFd(fd);

    // Prevent slow clients from blocking the accept thread.
    if (builtin.os.tag == .linux) {
        const timeout = posix.timeval{ .sec = 5, .usec = 0 };
        posix.setsockopt(fd, posix.SOL.SOCKET, posix.SO.RCVTIMEO, std.mem.asBytes(&timeout)) catch {};
        posix.setsockopt(fd, posix.SOL.SOCKET, posix.SO.SNDTIMEO, std.mem.asBytes(&timeout)) catch {};
    }

    var req_buf: [2048]u8 = undefined;
    const req_len = posix.read(fd, &req_buf) catch return;
    if (req_len == 0) return;

    const request = req_buf[0..req_len];
    if (!isGetRequest(request)) {
        writeSimpleResponse(fd, "405 Method Not Allowed", "text/plain", "method not allowed\n");
        return;
    }
    if (!isGetMetrics(request)) {
        writeSimpleResponse(fd, "404 Not Found", "text/plain", "not found\n");
        return;
    }
    writeMetricsResponse(fd, state) catch {
        writeSimpleResponse(fd, "500 Internal Server Error", "text/plain", "internal error\n");
    };
}

fn closeFd(fd: posix.fd_t) void {
    while (true) {
        switch (posix.errno(posix.system.close(fd))) {
            .SUCCESS => return,
            .INTR => continue,
            else => return,
        }
    }
}

fn writeMetricsResponse(fd: posix.fd_t, state: *proxy.ProxyState) !void {
    var body_buf: [32 * 1024]u8 = undefined;
    var body_writer: std.Io.Writer = .fixed(&body_buf);
    try writeMetrics(&body_writer, state, collectProcessMetrics());
    const body = body_writer.buffered();

    var header_buf: [256]u8 = undefined;
    var header_writer: std.Io.Writer = .fixed(&header_buf);
    try header_writer.print(
        "HTTP/1.1 200 OK\r\nConnection: close\r\nContent-Type: {s}\r\nContent-Length: {d}\r\n\r\n",
        .{ metrics_content_type, body.len },
    );
    try writeAll(fd, header_writer.buffered());
    try writeAll(fd, body);
}

fn writeSimpleResponse(fd: posix.fd_t, status: []const u8, content_type: []const u8, body: []const u8) void {
    var buf: [512]u8 = undefined;
    var writer: std.Io.Writer = .fixed(&buf);
    writer.print(
        "HTTP/1.1 {s}\r\nConnection: close\r\nContent-Type: {s}\r\nContent-Length: {d}\r\n\r\n{s}",
        .{ status, content_type, body.len, body },
    ) catch return;
    writeAll(fd, writer.buffered()) catch {};
}

fn writeAll(fd: posix.fd_t, bytes: []const u8) !void {
    var off: usize = 0;
    while (off < bytes.len) {
        const rc = linux.write(fd, bytes[off..].ptr, bytes.len - off);
        switch (linux.errno(rc)) {
            .SUCCESS => {
                if (rc == 0) return error.WriteFailed;
                off += rc;
            },
            .INTR => continue,
            else => return error.WriteFailed,
        }
    }
}

fn isGetRequest(request: []const u8) bool {
    return std.mem.startsWith(u8, request, "GET ");
}

fn isGetMetrics(request: []const u8) bool {
    if (!std.mem.startsWith(u8, request, "GET /metrics")) return false;
    if (request.len < "GET /metrics".len + 1) return false;
    const next = request["GET /metrics".len];
    return next == ' ' or next == '?' or next == '\r';
}

fn writeMetrics(writer: anytype, state: *proxy.ProxyState, process: ProcessMetrics) !void {
    const snapshot = state.getMetricsSnapshot();

    try writeMetricHeader(writer, "mtproto_build_info", "build and version metadata", "gauge");
    try writer.print("mtproto_build_info{{version=\"{s}\"}} 1\n", .{version});

    try writeGauge(writer, "mtproto_start_time_seconds", "proxy process start time", snapshot.start_time_seconds);
    try writeGauge(writer, "mtproto_uptime_seconds", "proxy process uptime", snapshot.uptime_seconds);
    try writeGauge(writer, "mtproto_connections_active", "current active client connections", snapshot.connections_active);
    try writeGauge(writer, "mtproto_connections_max", "configured maximum concurrent connections", snapshot.connections_max);
    try writeGauge(writer, "mtproto_handshakes_inflight", "current handshake budget usage", snapshot.handshakes_inflight);
    try writeCounter(writer, "mtproto_connections_accepted_total", "accepted client connections", snapshot.connections_accepted_total);
    try writeCounter(writer, "mtproto_connections_closed_total", "closed client connections", snapshot.connections_closed_total);
    try writeCounter(writer, "mtproto_connections_total", "total accepted client connections", snapshot.connections_total);
    try writeGauge(writer, "mtproto_accept_paused", "whether accepts are paused due to fd pressure", boolToInt(snapshot.accept_paused));
    try writeGauge(writer, "mtproto_saturation_paused", "whether accepts are paused due to saturation", boolToInt(snapshot.saturation_paused));
    try writeCounter(writer, "mtproto_drops_capacity_total", "connections dropped because max_connections was reached", snapshot.drops_capacity_total);
    try writeCounter(writer, "mtproto_drops_saturation_total", "accept attempts dropped due to saturation hysteresis", snapshot.drops_saturation_total);
    try writeCounter(writer, "mtproto_drops_rate_limit_total", "connections dropped by subnet rate limiter", snapshot.drops_rate_limit_total);
    try writeCounter(writer, "mtproto_drops_handshake_budget_total", "connections dropped because handshake budget was exhausted", snapshot.drops_handshake_budget_total);
    try writeCounter(writer, "mtproto_handshake_timeouts_total", "connections dropped due to handshake timeout", snapshot.handshake_timeouts_total);
    try writeCounter(writer, "mtproto_middleproxy_fallback_total", "times middleproxy fell back to direct path", snapshot.middleproxy_fallback_total);
    try writeCounter(writer, "mtproto_client_to_upstream_bytes_total", "bytes successfully written from client side toward upstream", snapshot.client_to_upstream_bytes_total);
    try writeCounter(writer, "mtproto_upstream_to_client_bytes_total", "bytes successfully written from upstream toward client side", snapshot.upstream_to_client_bytes_total);
    try writeGauge(writer, "mtproto_config_max_connections", "configured max_connections", snapshot.config_max_connections);
    try writeGauge(writer, "mtproto_config_port", "configured MTProto listen port", snapshot.config_port);
    try writeGauge(writer, "mtproto_middleproxy_enabled", "whether middleproxy mode is enabled", boolToInt(snapshot.middleproxy_enabled));
    try writeGauge(writer, "mtproto_fast_mode_enabled", "whether fast mode is enabled", boolToInt(snapshot.fast_mode_enabled));
    try writeGauge(writer, "mtproto_mask_enabled", "whether masking is enabled", boolToInt(snapshot.mask_enabled));
    try writeGauge(writer, "mtproto_desync_enabled", "whether desync is enabled", boolToInt(snapshot.desync_enabled));
    try writeGauge(writer, "mtproto_drs_enabled", "whether dynamic record sizing is enabled", boolToInt(snapshot.drs_enabled));
    try writePerUserMetrics(writer, state);
    try writeShadowsocksMetrics(writer, state);

    if (process.resident_memory_bytes) |value| {
        try writeGauge(writer, "process_resident_memory_bytes", "resident set size", value);
    }
    if (process.virtual_memory_bytes) |value| {
        try writeGauge(writer, "process_virtual_memory_bytes", "virtual memory size", value);
    }
    if (process.cpu_seconds_total) |value| {
        try writeMetricHeader(writer, "process_cpu_seconds_total", "user and system CPU time", "counter");
        try writer.print("process_cpu_seconds_total {d:.6}\n", .{value});
    }
    if (process.open_fds) |value| {
        try writeGauge(writer, "process_open_fds", "open file descriptors", value);
    }
    if (process.max_fds) |value| {
        try writeGauge(writer, "process_max_fds", "maximum file descriptors", value);
    }
    if (process.cgroup_memory_usage_bytes) |value| {
        try writeGauge(writer, "mtproto_cgroup_memory_usage_bytes", "memory usage reported by cgroup", value);
    }
    if (process.cgroup_memory_limit_bytes) |value| {
        try writeGauge(writer, "mtproto_cgroup_memory_limit_bytes", "memory limit reported by cgroup", value);
    }
}

fn writeMetricHeader(writer: anytype, name: []const u8, help: []const u8, metric_type: []const u8) !void {
    try writer.print("# HELP {s} {s}\n", .{ name, help });
    try writer.print("# TYPE {s} {s}\n", .{ name, metric_type });
}

fn writePerUserMetrics(writer: anytype, state: *proxy.ProxyState) !void {
    try writeMetricHeader(writer, "mtproto_user_connections_active", "active connections by configured user", "gauge");
    for (state.user_metrics) |entry| {
        try writeLabeledMetricLine(
            writer,
            "mtproto_user_connections_active",
            entry.name,
            entry.connections_active.load(.monotonic),
        );
    }

    try writeMetricHeader(writer, "mtproto_user_client_to_upstream_bytes_total", "bytes successfully written upstream by configured user", "counter");
    for (state.user_metrics) |entry| {
        try writeLabeledMetricLine(
            writer,
            "mtproto_user_client_to_upstream_bytes_total",
            entry.name,
            entry.client_to_upstream_bytes_total.load(.monotonic),
        );
    }

    try writeMetricHeader(writer, "mtproto_user_upstream_to_client_bytes_total", "bytes successfully written to client by configured user", "counter");
    for (state.user_metrics) |entry| {
        try writeLabeledMetricLine(
            writer,
            "mtproto_user_upstream_to_client_bytes_total",
            entry.name,
            entry.upstream_to_client_bytes_total.load(.monotonic),
        );
    }
}

fn writeShadowsocksMetrics(writer: anytype, state: *proxy.ProxyState) !void {
    const ss_cfg = state.config.shadowsocks;
    try writeGauge(writer, "mtproto_ss_enabled", "whether the Shadowsocks-2022 listener is enabled and configured", boolToInt(ss_cfg.isUsable()));
    try writeGauge(writer, "mtproto_ss_port", "configured Shadowsocks-2022 listen port", ss_cfg.port);
    try writeGauge(writer, "mtproto_ss_allowed_hosts_count", "number of suffixes in the SS allow-list", ss_cfg.allowed_hosts.len);
    try writeGauge(writer, "mtproto_ss_block_private", "whether SS blocks RFC1918/loopback/ULA targets", boolToInt(ss_cfg.block_private_networks));

    try writeCounter(writer, "mtproto_ss_connections_accepted_total", "Shadowsocks-2022 connections accepted on the listener", state.stats_ss_accepted.load(.monotonic));
    try writeCounter(writer, "mtproto_ss_handshake_failed_total", "SS sessions terminated during AEAD handshake", state.stats_ss_handshake_failed.load(.monotonic));
    try writeCounter(writer, "mtproto_ss_replay_total", "SS sessions rejected by the salt anti-replay cache", state.stats_ss_replay.load(.monotonic));
    try writeCounter(writer, "mtproto_ss_blocked_host_total", "SS CONNECT requests rejected by the host whitelist", state.stats_ss_blocked_host.load(.monotonic));
    try writeCounter(writer, "mtproto_ss_blocked_private_total", "SS CONNECT requests rejected because the target was a private network", state.stats_ss_blocked_private.load(.monotonic));
    try writeCounter(writer, "mtproto_ss_dns_failed_total", "SS CONNECT attempts where DNS resolution failed", state.stats_ss_dns_failed.load(.monotonic));
    try writeCounter(writer, "mtproto_ss_relayed_total", "SS sessions that successfully reached the relay phase", state.stats_ss_relayed.load(.monotonic));
    try writeCounter(writer, "mtproto_ss_client_to_upstream_bytes_total", "plaintext bytes forwarded from SS client to upstream", state.stats_ss_c2u_bytes.load(.monotonic));
    try writeCounter(writer, "mtproto_ss_upstream_to_client_bytes_total", "plaintext bytes forwarded from upstream to SS client (pre-AEAD)", state.stats_ss_u2c_bytes.load(.monotonic));
}

fn writeLabeledMetricLine(writer: anytype, metric_name: []const u8, user_name: []const u8, value: anytype) !void {
    try writer.print("{s}{{user=\"", .{metric_name});
    try writePrometheusLabelValue(writer, user_name);
    try writer.print("\"}} {d}\n", .{value});
}

fn writePrometheusLabelValue(writer: anytype, value: []const u8) !void {
    for (value) |ch| {
        switch (ch) {
            '\\' => try writer.writeAll("\\\\"),
            '"' => try writer.writeAll("\\\""),
            '\n' => try writer.writeAll("\\n"),
            else => try writer.writeByte(ch),
        }
    }
}

fn writeGauge(writer: anytype, name: []const u8, help: []const u8, value: anytype) !void {
    try writeMetricHeader(writer, name, help, "gauge");
    try writer.print("{s} {d}\n", .{ name, value });
}

fn writeCounter(writer: anytype, name: []const u8, help: []const u8, value: anytype) !void {
    try writeMetricHeader(writer, name, help, "counter");
    try writer.print("{s} {d}\n", .{ name, value });
}

fn boolToInt(value: bool) u8 {
    return if (value) 1 else 0;
}

fn collectProcessMetrics() ProcessMetrics {
    return .{
        .resident_memory_bytes = readStatusValueBytes("VmRSS:"),
        .virtual_memory_bytes = readStatusValueBytes("VmSize:"),
        .cpu_seconds_total = readCpuSecondsTotal(),
        .open_fds = countOpenFds(),
        .max_fds = readMaxFds(),
        .cgroup_memory_usage_bytes = readCgroupMemoryCurrent(),
        .cgroup_memory_limit_bytes = readCgroupMemoryLimit(),
    };
}

fn readCgroupMemoryCurrent() ?u64 {
    return readNumericFileAbsolute("/sys/fs/cgroup/memory.current") orelse
        readNumericFileAbsolute("/sys/fs/cgroup/memory/memory.usage_in_bytes");
}

fn readCgroupMemoryLimit() ?u64 {
    return readCgroupMemoryLimitFile("/sys/fs/cgroup/memory.max") orelse
        readCgroupMemoryLimitFile("/sys/fs/cgroup/memory/memory.limit_in_bytes");
}

fn readCgroupMemoryLimitFile(path: []const u8) ?u64 {
    var buf: [256]u8 = undefined;
    const text = readFileAbsolute(path, &buf) orelse return null;
    const trimmed = std.mem.trim(u8, text, " \t\r\n");
    if (trimmed.len == 0 or std.mem.eql(u8, trimmed, "max")) return null;
    return std.fmt.parseInt(u64, trimmed, 10) catch null;
}

fn readNumericFileAbsolute(path: []const u8) ?u64 {
    var buf: [256]u8 = undefined;
    const text = readFileAbsolute(path, &buf) orelse return null;
    const trimmed = std.mem.trim(u8, text, " \t\r\n");
    if (trimmed.len == 0) return null;
    return std.fmt.parseInt(u64, trimmed, 10) catch null;
}

fn readStatusValueBytes(label: []const u8) ?u64 {
    var buf: [16 * 1024]u8 = undefined;
    const text = readFileAbsolute("/proc/self/status", &buf) orelse return null;

    var lines = std.mem.splitScalar(u8, text, '\n');
    while (lines.next()) |line| {
        if (!std.mem.startsWith(u8, line, label)) continue;
        var it = std.mem.tokenizeAny(u8, line[label.len..], " \t");
        const value_txt = it.next() orelse return null;
        const kib = std.fmt.parseInt(u64, value_txt, 10) catch return null;
        return kib * 1024;
    }
    return null;
}

fn readCpuSecondsTotal() ?f64 {
    var buf: [8 * 1024]u8 = undefined;
    const text = readFileAbsolute("/proc/self/stat", &buf) orelse return null;
    const close_idx = std.mem.lastIndexOfScalar(u8, text, ')') orelse return null;
    if (close_idx + 2 >= text.len) return null;

    var fields = std.mem.tokenizeScalar(u8, text[close_idx + 2 ..], ' ');
    var idx: usize = 0;
    var utime_ticks: ?u64 = null;
    var stime_ticks: ?u64 = null;
    while (fields.next()) |field| : (idx += 1) {
        if (idx == 11) {
            utime_ticks = std.fmt.parseInt(u64, field, 10) catch return null;
        } else if (idx == 12) {
            stime_ticks = std.fmt.parseInt(u64, field, 10) catch return null;
            break;
        }
    }
    if (utime_ticks == null or stime_ticks == null) return null;
    return @as(f64, @floatFromInt(utime_ticks.? + stime_ticks.?)) / 100.0;
}

fn countOpenFds() ?u64 {
    const io_ctx = std.Io.Threaded.global_single_threaded.io();
    var dir = std.Io.Dir.openDirAbsolute(io_ctx, "/proc/self/fd", .{ .iterate = true }) catch return null;
    defer dir.close(io_ctx);

    var it = dir.iterate();
    var count: u64 = 0;
    while (it.next(io_ctx) catch return null) |_| {
        count += 1;
    }
    return count;
}

fn readMaxFds() ?u64 {
    if (builtin.os.tag != .linux) return null;

    var lim: linux.rlimit = undefined;
    const rc = linux.getrlimit(.NOFILE, &lim);
    switch (posix.errno(rc)) {
        .SUCCESS => return @intCast(lim.cur),
        else => return null,
    }
}

fn readFileAbsolute(path: []const u8, buffer: []u8) ?[]const u8 {
    const io_ctx = std.Io.Threaded.global_single_threaded.io();
    var file = std.Io.Dir.openFileAbsolute(io_ctx, path, .{}) catch return null;
    defer file.close(io_ctx);
    var reader = file.reader(io_ctx, &.{});
    const content = reader.interface.allocRemaining(std.heap.page_allocator, .limited(buffer.len)) catch return null;
    defer std.heap.page_allocator.free(content);
    @memcpy(buffer[0..content.len], content);
    return buffer[0..content.len];
}

test "metrics output contains required metrics" {
    var cfg = config.Config{
        .users = std.StringHashMap([16]u8).init(std.testing.allocator),
        .direct_users = std.StringHashMap(void).init(std.testing.allocator),
    };
    defer cfg.deinit(std.testing.allocator);
    try cfg.users.put(try std.testing.allocator.dupe(u8, "test"), [_]u8{0x11} ** 16);

    var state = try proxy.ProxyState.init(std.testing.allocator, cfg, "test-config.toml");
    defer state.deinit();

    var buf: [32 * 1024]u8 = undefined;
    var writer: std.Io.Writer = .fixed(&buf);
    try writeMetrics(&writer, &state, .{});
    const out = writer.buffered();
    try std.testing.expect(std.mem.indexOf(u8, out, "mtproto_connections_active") != null);
    try std.testing.expect(std.mem.indexOf(u8, out, "mtproto_build_info") != null);
    try std.testing.expect(std.mem.indexOf(u8, out, "mtproto_client_to_upstream_bytes_total") != null);
    // Shadowsocks metrics are always emitted (zero values when disabled).
    try std.testing.expect(std.mem.indexOf(u8, out, "mtproto_ss_enabled") != null);
    try std.testing.expect(std.mem.indexOf(u8, out, "mtproto_ss_connections_accepted_total") != null);
    try std.testing.expect(std.mem.indexOf(u8, out, "mtproto_ss_blocked_host_total") != null);
    try std.testing.expect(std.mem.indexOf(u8, out, "mtproto_ss_relayed_total") != null);
}

test "metrics rejects unknown path" {
    var cfg = config.Config{
        .users = std.StringHashMap([16]u8).init(std.testing.allocator),
        .direct_users = std.StringHashMap(void).init(std.testing.allocator),
    };
    defer cfg.deinit(std.testing.allocator);
    try cfg.users.put(try std.testing.allocator.dupe(u8, "test"), [_]u8{0x22} ** 16);

    var state = try proxy.ProxyState.init(std.testing.allocator, cfg, "test-config.toml");
    defer state.deinit();

    try std.testing.expect(!isGetMetrics("GET /nope HTTP/1.1\r\nHost: localhost\r\n\r\n"));
    try std.testing.expect(isGetRequest("GET /nope HTTP/1.1\r\nHost: localhost\r\n\r\n"));
}
